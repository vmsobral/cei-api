buildscript {
    ext.kotlin_version = '1.3.41'
    ext.spring_version = '5.1.9.RELEASE'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
    }
}

allprojects {
    ext {
        events_version = '2.2.0'
    }
}

subprojects {
    version = '1.0'
    group = 'br.com'

    apply plugin: 'kotlin'
    apply plugin: 'kotlin-spring'
    apply plugin: 'jacoco'

    sourceCompatibility = 1.8

    jacoco {
        toolVersion = '0.8.4'
        reportsDir = file("$buildDir/reports/jacoco")
    }

    jacocoTestReport {
        reports {
            xml.enabled = true
            csv.enabled = false
        }
    }

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: "$kotlin_version"
        compile group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: "$kotlin_version"

        compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
        compile group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: '6.1'

        compile group: 'org.springframework', name: 'spring-context', version: "$spring_version"

        compile group: 'io.opentracing', name: 'opentracing-api', version: '0.33.0'
        compile group: 'io.opentracing', name: 'opentracing-util', version: '0.33.0'
        compile group: 'com.datadoghq', name: 'dd-trace-ot', version: '0.31.0'
        compile group: 'com.datadoghq', name: 'dd-trace-api', version: '0.31.0'

        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.5.1'
        testCompile group: 'com.nhaarman.mockitokotlin2', name: 'mockito-kotlin', version: '2.1.0'
        testCompile group: 'org.springframework', name: 'spring-test', version: "$spring_version"
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
    }

    compileKotlin {
        kotlinOptions {
            allWarningsAsErrors = true
            jvmTarget = "1.8"
        }
    }
}
